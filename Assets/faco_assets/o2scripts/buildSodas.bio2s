/*
Description:
Build 16 soda cans at once

Functionality:
1/ create your P: for O2
2/ create P:\z and P:\z\addons
3/ unpbo and copy dayz_communityassets to P:\z\addons\dayz_communityassets
4/ open soda_generic_flip_empty.p3d in O2
5/ menu automation > scripts > run script, search buildSodas.bio2s, then click on "Save" in order to load the script
6/ click "Ok" to overwrite empty soda cans in P:\z\addons\dayz_communityassets\models

you can do the same with soda_generic_flip_full.p3d, in this case the 16 full soda cans will be built

Licensing Info:
(c) facoptere@gmail.com - for DayZMod 

*/

_sodas =[
// texture                 		mean color   			model to save 
 ["soda_clays_clean_co.paa",		"0.66,0.56,0.31,1",		"soda_clays_clean"]
,
 ["soda_franka_clean_co.paa",	"0.72,0.49,0.33,1",		"soda_franka_clean"]
,
 ["soda_lirik_clean_co.paa",		"0.73,0.35,0.36,1",		"soda_lirik_clean"]
,
 ["soda_mtngreen_clean_co.paa",	"0.27,0.51,0.31,1",		"soda_mtngreen_clean"]
,
 ["soda_mzly_clean_co.paa",		"0.41,0.59,0.20,1",		"soda_mzly_clean"]
,
 ["soda_peppsy_clean_co.paa",	"0.23,0.37,0.55,1",		"soda_peppsy_clean"]
,
 ["soda_rocket_fuel_co.paa",		"0.82,0.76,0.79,1",		"soda_rocket_clean"]
,
 ["soda_sherbet_clean_co.paa",			"0.80,0.48,0.21,1",		"soda_sherbet_clean"]
,
 ["soda_smasht_clean_co.paa",	"0.40,0.29,0.48,1",		"soda_smasht_clean"]
,
 ["soda_drwaste_clean_co.paa",	"0.65,0.20,0.15,1",		"soda_drwaste_clean"]
,
 ["soda_sacrite_clean_co.paa",	"0.23,0.51,0.52,1",		"soda_sacrite_clean"]
,
 ["soda_lvg_clean_co.paa",		"0.55,0.32,0.22,1",		"soda_lvg_clean"]
,
 ["soda_grape_drink_co.paa",		"0.35,0.55,0.41,1",		"soda_grape_clean"]
,
 ["soda_rabbit_clean_co.paa",	"0.34,0.51,0.39,1",		"soda_rabbit_clean"]
,
 ["soda_r4z0r_clean_co.paa",		"0.28,0.65,0.38,1",		"soda_r4z0r_clean"]
,
 ["soda_lemonade_clean_co.paa",	"0.63,0.65,0.48,1",		"soda_lemonade_clean"]
//,
//["soda_rusty_co.paa",			"0.57,0.23,0.05,1",		"soda_rusty"]
];

_p3d_write_path = '\z\addons\dayz_communityassets\models\';
_paa_path = 'z\addons\dayz_communityassets\textures\';

_suffix = ((splitPath nameOf this) @ -2) @ [ -5, -5 ];
_suffix = if (_suffix == "_full") then { "_full" } else { "_empty" };

scopeName "mainScope";
//#include "std\lodNames.inc"
//console = openStandardIO;
//if (this in ["options"]) exitWith {messageBox ["No options available for this tool.", 0];};
//_initLevel = (activeLevel this);



_dlg = dialogBox
	[150, 50, "Soda can builder",
		["init", {}],
		["label",200,13,"Ready to overwrite all "+_suffix+" soda cans?",0],
		["break"],
		["ok-button", 45, 15],
		["cancel-button", 45, 15]
	];
	
echo str (_dlg); // 1=OK
if (_dlg == 1) then {
	{
		_fileto=_p3d_write_path+(_x select 2)+_suffix+".p3d";
		_colorto="#(argb,8,8,3)color("+(_x select 1)+",co)";
		_textureto=_paa_path+(_x select 0);
		echo ("Processing file "+_fileto+" with mean color "+_colorto+" and texture "+_textureto);
		_object=this as _fileto;
		// for each model LOD
		_levels = (getObjects _object);
		for "_u" from 0 to ((count _levels) - 1) do
		{
			_lod = _levels select _u;
			{	
				if (_x == "label") then {
					_mesh = _lod loadSelection _x;
					{
						_lod face _x setTexture(_textureto);
					} forEach getSelectedFaces _mesh;
				}
				else {
					if (_x == "labelfar") then {
						_mesh = _lod loadSelection _x;
						{
							_lod face _x setTexture(_colorto);
						} forEach getSelectedFaces _mesh;
					};
				};
			} forEach getSelections _lod;
		};
		save _object;
	} forEach _sodas;
	messageBox ["Done!",0];
};

breakOut "mainScope";